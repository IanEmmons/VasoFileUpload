plugins {
	id 'org.springframework.boot' version '3.2.2'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'java'
	id 'eclipse'
}

group = 'org.virginiaso'
version = '1.4.4'

ext {
	eol = "${System.lineSeparator}"
	genRsrcDir = file("$buildDir/generated/resources")
	projPropsRsrcFile = file("$genRsrcDir/project-info.properties")
}

repositories {
	mavenCentral()
}

dependencies {
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	implementation 'javax.annotation:javax.annotation-api:1.3.2'
	implementation 'org.apache.commons:commons-csv:1.10.0'
	implementation 'org.apache.commons:commons-lang3:3.14.0'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'software.amazon.awssdk:s3:2.23.7'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

tasks.withType(JavaCompile) {
	options.compilerArgs += '-Xlint:all'
}

sourceSets {
	main {
		resources { srcDir genRsrcDir }
	}
}

task generateProjectPropertiesRsrc {
	outputs.file(projPropsRsrcFile)
	doLast {
		projPropsRsrcFile.text = "projectName=${project.name}${eol}projectVersion=${project.version}${eol}"
	}
}

processResources.dependsOn generateProjectPropertiesRsrc

testing {
	suites {
		test {
			useJUnitJupiter('5.10.1')
		}
	}
}
